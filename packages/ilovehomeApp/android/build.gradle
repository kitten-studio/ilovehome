// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "31.0.0"
        minSdkVersion = 21
        compileSdkVersion = 33
        targetSdkVersion = 33
        // Only using Android Support libraries
        supportLibVersion = "28.0.0"
        firebaseBomVersion = "31.3.0"
        googlePlayServicesVersion = "17.0.0"

        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }
        // https://rnfirebase.io/#android
        set("react-native", [
            versions: [
                // Overriding Build/Android SDK Versions
                android : [
                    minSdk    : minSdkVersion,
                    targetSdk : targetSdkVersion,
                    compileSdk: compileSdkVersion,
                    buildTools: buildToolsVersion
                ],
                // Overriding Library SDK Versions
                firebase: [
                    // Override Firebase SDK Version
                    bom: firebaseBomVersion
                ],
            ],
        ])
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.2.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("de.undercouch:gradle-download-task:5.0.1")
        classpath("com.google.gms:google-services:4.3.15")
        classpath("com.google.firebase:firebase-crashlytics-gradle:2.9.8")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def REACT_NATIVE_VERSION = new File(['node', '--print',"JSON.parse(require('fs').readFileSync(require.resolve('react-native/package.json'), 'utf-8')).version"].execute(null, rootDir).text.trim())

allprojects {
    repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../../../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../../../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        jcenter()
        maven { url 'https://www.jitpack.io' }
    }
    configurations.all {
        resolutionStrategy {
            // Patch needed for https://github.com/facebook/react-native/issues/35210
            // This is a patch to short-circuit the "+" dependencies inside the
            // users' app/build.gradle file and the various .gradle files of libraries.
            // As using plain "+" dependencies causes Gradle to always download the latest,
            // this logic forces Gradle to use latest release in the minor series.
            force "com.facebook.react:react-native:" + REACT_NATIVE_VERSION
            force "com.facebook.react:hermes-engine:" + REACT_NATIVE_VERSION
        }
    }
}

subprojects {
    buildscript.repositories {
        all { repository ->
            if (repository.name == "BintrayJCenter") {
                remove repository
                if (findByName("MavenRepo") == null) mavenCentral()
            }
        }
    }
    afterEvaluate { evaluated ->
        if (evaluated.hasProperty('android')) {
            android {
                // 불필요한 SDK 요소 설치 방지
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}

